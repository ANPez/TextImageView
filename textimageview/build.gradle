apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

/*
ext {
  bintrayRepo = 'maven'
  bintrayName = 'textimageview'

  publishedGroupId = 'com.antonionicolaspina'
  libraryName = 'TextImageView'
  artifact = 'textimageview'

  libraryDescription = 'An ImageView subclass that draws one or more spannable, rotable, scalable texts on top of the image'

  siteUrl = 'https://github.com/ANPez/TextImageView'
  gitUrl = 'https://github.com/ANPez/TextImageView.git'

  libraryVersion = '3.1.2'

  developerId = 'anpez'
  developerName = 'Antonio Nicol√°s Pina'
  developerEmail = 'antonio@antonionicolaspina.com'

  licenseName = 'The Apache Software License, Version 2.0'
  licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
  allLicenses = ["Apache-2.0"]
}
*/
//region Publish to jCenter
def libGroupId = 'com.antonionicolaspina'
def libArtifactId = 'textimageview'
def libVersion = '3.1.2'
def libDesc = 'An ImageView subclass that draws one or more spannable, rotable, scalable texts on top of the image.'
def libLicenses = ['Apache-2.0']
def libVCSUrl = 'https://github.com/ANPez/TextImageView.git'

// Generate Source Jar
task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier "sources"
}

// Create the publication with the pom configuration
publishing {
  publications {
    MyPublication(MavenPublication) {
      groupId libGroupId
      artifactId libArtifactId
      version libVersion
      artifact(sourceJar)
      artifact("$buildDir/outputs/aar/$libArtifactId-release.aar")
    }
  }
}

// Bintray config
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")
  publications = ['MyPublication']
  pkg {
    repo = "maven"
    name = "$libGroupId:$libArtifactId"
    desc = libDesc
    licenses = libLicenses
    vcsUrl = libVCSUrl
    version {
      name = libVersion
      vcsTag = libVersion
      released = new Date()
    }
  }
}
//endregion

android {
  compileSdkVersion 30
  buildToolsVersion "29.0.2"

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 30
    versionCode 11
    versionName "3.1.2"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  testImplementation 'junit:junit:4.12'
  implementation "androidx.core:core-ktx:1.3.1"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
